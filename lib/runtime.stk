;;;;
;;;; runtime.stk	-- Stuff necessary for bootstaping the system
;;;; 
;;;; Copyright © 2001-2007 Erick Gallesio - I3S-CNRS/ESSI <eg@unice.fr>
;;;; 
;;;; 
;;;; This program is free software; you can redistribute it and/or modify
;;;; it under the terms of the GNU General Public License as published by
;;;; the Free Software Foundation; either version 2 of the License, or
;;;; (at your option) any later version.
;;;; 
;;;; This program is distributed in the hope that it will be useful,
;;;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;;;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;;;; GNU General Public License for more details.
;;;; 
;;;; You should have received a copy of the GNU General Public License
;;;; along with this program; if not, write to the Free Software
;;;; Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, 
;;;; USA.
;;;; 
;;;;           Author: Erick Gallesio [eg@unice.fr]
;;;;    Creation date: 15-Mar-2001 22:49 (eg)
;;;; Last file update: 25-Jan-2007 19:20 (eg)
;;;;


(define (map* fn . l) 		; A map which accepts dotted lists (arg lists  
  (cond 			; must be "isomorph"
   ((null? (car l)) '())
   ((pair? (car l)) (cons (apply fn      (map car l))
			  (apply map* fn (map cdr l))))
   (else            (apply fn l))))

(define (for-each* fn . l) 	; A for-each which accepts dotted lists (arg lists  
  (cond 			; must be "isomorph"
   ((null? (car l)) '())
   ((pair? (car l)) (apply fn (map car l)) (apply for-each* fn (map cdr l)))
   (else            (apply fn l))))

;;; ----------------------------------------------------------------------
;;;
;;; 				E x p a n d e r s 
;;;
;;; ----------------------------------------------------------------------
(define *expander-list*     '())	; the macros
(define *expander-list-src* '())	; their source code 	

(define (expander? x)
  (assq x *expander-list*))


(define (application-expander x e)
  (map* (lambda (y) (e y e)) x))


(define (initial-expander x e)
 (cond 
   ((not (pair? x))   	      x)
   ((not (symbol? (car x)))  (application-expander x e))
   (else  (let ((functor (car x)))
	    (cond 
	       ((expander? functor) ((cdr (assq functor *expander-list*)) x e))
	       (else (application-expander x e)))))))


(define (install-expander! id proc :optional code)
  (set! *expander-list* (cons (cons id proc) *expander-list*))
  ;; Keep the code associated to the macro to save it in byte-code header
  (when code
    (set! *expander-list-src* (cons (cons id code) *expander-list-src*))))
    
	

;;(define (remove-expander! id)
;;  (let ((exp (assq id *expander-list*)))
;;    (set! *expander-list* (delete! exp *expander-list* eq?))))


;;;
;;; Runtime support for R5RS macro
;;;
(define (syntax-expand expr) expr)

(define (macro-expand x)
  (initial-expander (syntax-expand x) initial-expander))

(define (macro-expand* exp)
  (let ((new (macro-expand exp)))
    (if (equal? new exp)
        new
        (macro-expand* new))))
