;;;;
;;;; http.stk	-- Minimal HTTP management for snowman
;;;; 
;;;; Copyright © 2007 Erick Gallesio - I3S-CNRS/ESSI <eg@essi.fr>
;;;; 
;;;; 
;;;; This program is free software; you can redistribute it and/or modify
;;;; it under the terms of the GNU General Public License as published by
;;;; the Free Software Foundation; either version 2 of the License, or
;;;; (at your option) any later version.
;;;; 
;;;; This program is distributed in the hope that it will be useful,
;;;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;;;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;;;; GNU General Public License for more details.
;;;; 
;;;; You should have received a copy of the GNU General Public License
;;;; along with this program; if not, write to the Free Software
;;;; Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, 
;;;; USA.
;;;; 
;;;;           Author: Erick Gallesio [eg@essi.fr]
;;;;    Creation date: 12-Jan-2007 09:03 (eg)
;;;; Last file update: 19-Jan-2007 11:43 (eg)
;;;;

(define (http-get url output)
  ;; Copy the content of url to the output port

  (define (skip-header port)
    (let loop ((line (read-line port)))
      (unless (or (eof-object? line) (string=? line ""))
	(loop (read-line port)))))

  (define (copy-url server port user path)
    (let* ((s   (make-client-socket server port))
	   (out (socket-output s))
	   (in  (socket-input s)))
      ;; Send HTTP request
      (fprintf out "GET ~a HTTP/1.0\r\n" path)
      (fprintf out "Host: ~a\r\n" server)
      (fprintf out "Port: ~a\r\n" port)
      (when user
	(fprintf out "Authorization: Basic ~a\r\n" (base64-encode-string user)))
      (fprintf out "Connection: close\r\n")
      (fprintf out "\r\n")
      (flush-output-port out)

      ;; Read header
      (let ((line (read-line in)))
	(if (regexp-match "[Hh][Tt][Tt][Pp].* +200 +.*" line)
	    ;; The request is correct. Skip the header
	    (skip-header in)
	    (error "cannot get the document at url ~s. code ~s" url line)))

      ;; copy the content of the url on output
      (copy-port in output)
      (socket-shutdown s #t)))
    
  (let ((info (uri-parse url)))
    (unless (equal? (key-get info :scheme) "http")
      (error "bad url (protocol is not http) ~S" url))
    (let ((user (key-get info :user #f))
	  (host (key-get info :host))
	  (port (key-get info :port))
	  (path (key-get info :path)))
      (copy-url host port user path))))
      
